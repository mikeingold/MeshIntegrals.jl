################################################################################
#                           Integration Rules
################################################################################

abstract type IntegrationRule end

"""
    GaussKronrod(kwargs...)

The h-adaptive Gauss-Kronrod quadrature rule implemented by
[QuadGK.jl](https://github.com/JuliaMath/QuadGK.jl). All standard
`QuadGK.quadgk` keyword arguments are supported. This rule works natively for one
dimensional geometries; some two- and three-dimensional geometries are additionally
supported using nested integral solvers with the specified `kwarg` settings.
"""
struct GaussKronrod <: IntegrationRule
    kwargs::Base.Pairs
    GaussKronrod(; kwargs...) = new(kwargs)
end

"""
    GaussLegendre(n)

An `n`'th-order Gauss-Legendre quadrature rule. Nodes and weights are
efficiently calculated using
[FastGaussQuadrature.jl](https://github.com/JuliaApproximation/FastGaussQuadrature.jl).

So long as the integrand function can be well-approximated by a polynomial of
order `2n-1`, this method should yield results with 16-digit accuracy in `O(n)`
time. If the function is know to have some periodic content, then `n` should
(at a minimum) be greater than the expected number of periods over the geometry,
e.g. `length(geometry)/Î»`.
"""
struct GaussLegendre <: IntegrationRule
    n::Int64
    nodes::Vector{Float64}
    weights::Vector{Float64}

    GaussLegendre(n::Int64) = new(n, FastGaussQuadrature.gausslegendre(n)...)
end

"""
    HAdaptiveCubature(kwargs...)

The h-adaptive cubature rule implemented by
[HCubature.jl](https://github.com/JuliaMath/HCubature.jl). All standard
`HCubature.hcubature` keyword arguments are supported.
"""
struct HAdaptiveCubature <: IntegrationRule
    kwargs::Base.Pairs
    HAdaptiveCubature(; kwargs...) = new(kwargs)
end
